apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion versions.compileSdkVersion
  buildToolsVersion versions.buildToolsVersion
  defaultConfig {
    applicationId "com.littlegnal.accounting"
    minSdkVersion versions.minSdkVersion
    targetSdkVersion versions.targetSdkVersion
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      debuggable true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  kapt {
    correctErrorTypes = true
  }


  androidExtensions {
    experimental = true
  }
}

configurations {
  ktlint
}

task ktlint(type: JavaExec, group: LifecycleBasePlugin.VERIFICATION_GROUP) {
  inputs.dir('src')
  outputs.dir('src')

  description = "Check Kotlin code style."
  classpath = configurations.ktlint
  main = 'com.github.shyiko.ktlint.Main'
  args '--android', 'src/**/*.kt'
}
check.dependsOn ktlint

task ktFormat(type: JavaExec, group: 'formatting') {
  inputs.dir('src')
  outputs.dir('src')

  description = 'Fix Kotlin code style deviations.'
  classpath = configurations.ktlint
  main = 'com.github.shyiko.ktlint.Main'
  args '--android', '-F', 'src/**/*.kt'
}

dependencies {
  implementation libraries.kotlinStdlibJre7
  // support lib
  implementation libraries.supportAppcompatV7
  implementation libraries.supportDesign
  implementation libraries.constraintLayout
  // timber
  implementation libraries.timber
  // dagger
  implementation libraries.dagger2
  implementation libraries.dagger2Android
  implementation libraries.dagger2AndroidSupport
  kapt libraries.dagger2Compiler
  kapt libraries.dagger2AndroidCompiler
  // architecture components
  implementation libraries.archComponentRoom
  kapt libraries.archComponentRoomCompiler
  implementation libraries.archComponentRxJava2
  implementation libraries.arch_lifecycle_extensions
  // epoxy
  implementation libraries.epoxy
  kapt libraries.epoxyCompiler
  // rxjava
  implementation libraries.rxAndroid2
  implementation libraries.rxJava2
  // rxbinding
  implementation libraries.rxBindingRecyclerKotlin
  implementation libraries.flexbox
  // rxrelay
  implementation libraries.rxrelay

  // stetho
  debugImplementation libraries.stetho
  // leakcanary
  debugImplementation libraries.leakCanary
  releaseImplementation libraries.leakCanaryNoOp
  testImplementation libraries.leakCanaryNoOp
  debugImplementation libraries.leakCanarySupportFragment

  ktlint libraries.ktlint

  testImplementation test.junit
  testImplementation test.archComponentRoomTest
  testImplementation test.archComponentCoreTest
  testImplementation test.hamcrestAll
  testImplementation test.mockito

  androidTestImplementation test.junit
  androidTestImplementation test.support_test_runner
  androidTestImplementation test.archComponentRoomTest
  androidTestImplementation test.archComponentCoreTest
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'

  debugImplementation libraries.flipper
}
